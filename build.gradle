plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'me.champeau.jmh' version '0.7.3'
}

dependencies {
    implementation subprojects
}

allprojects {
    repositories {
        mavenCentral()
        maven { url = 'https://jitpack.io' }
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'me.champeau.jmh'

    project.ext.lwjglVersion = '3.3.6'
    project.ext.lwjglNatives = 'natives-windows'
    project.ext.jomlVersion = '1.10.8'

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.42'
        annotationProcessor 'org.projectlombok:lombok:1.18.42'

        testCompileOnly 'org.projectlombok:lombok:1.18.42'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.42'

        testImplementation 'org.junit.jupiter:junit-jupiter:6.0.1'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:6.0.1'
        testImplementation 'org.junit.jupiter:junit-jupiter-engine:6.0.1'
        testImplementation 'org.junit.jupiter:junit-jupiter-params:6.0.1'
        testImplementation 'org.junit.platform:junit-platform-commons:6.0.1'
        testImplementation 'org.junit.platform:junit-platform-engine:6.0.1'
        testImplementation 'org.mockito:mockito-core:5.20.0'
        testImplementation 'org.mockito:mockito-junit-jupiter:5.20.0'
        testImplementation 'org.assertj:assertj-core:3.27.6'

        jmh 'org.openjdk.jmh:jmh-core:1.37'
        jmh 'org.openjdk.jmh:jmh-generator-annprocess:1.37'
        jmhAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.37'
    }

    compileJava.options.encoding = compileTestJava.options.encoding = 'UTF-8'

    java {
        sourceCompatibility = targetCompatibility = JavaVersion.VERSION_21
    }

    configurations.configureEach {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }

    test {
        useJUnitPlatform()
    }

    bootJar {
        enabled = false
    }
}

tasks.register('createTestDirectories') {
    mkdir 'test/client'
    mkdir 'test/server'
    mkdir 'test/mongodb-service'
    mkdir 'test/editor'
    mkdir 'test/benchmark'
}

import com.github.jengelman.gradle.plugins.shadow.transformers.PropertiesFileTransformer

shadowJar {
    // Required for Spring
    mergeServiceFiles()
    append 'META-INF/spring.handlers'
    append 'META-INF/spring.schemas'
    append 'META-INF/spring.tooling'
    transform(PropertiesFileTransformer) {
        paths = ['META-INF/spring.factories']
        mergeStrategy = 'append'
    }
}

bootJar {
    enabled = false
}

jar {
    manifest {
        attributes 'Class-Path': configurations.runtimeClasspath.files.collect { it.getName() }.join(' '),
                'Main-Class': 'net.bfsr.client.launch.LWJGL3Main',
                'Multi-Release': true//log4j2 workaround
    }
}